// To learn how to write nodes, see https://coollab-art.com/Tutorials/Writing%20Nodes/Intro

INPUT UV->float 'Image';
INPUT float 'Resolution';
INPUT float 'Subdivision threshold';
INPUT int 'Max subdivisions';
INPUT float 'Roundness';

float squircle_sdf(vec2 uv, float radius)
{
    float power = 2. / 'Roundness';
    return pow(pow(abs(uv.x), power) + pow(abs(uv.y), power), 1. / power) - radius;
}

float sq(float x)
{
    return x * x;
}

SignedDistance main(UV uv)
{
    vec2 guv         = vec2(0.);
    vec2 grid_center = vec2(0.);

    int   it       = 0;
    float std_dev  = FLT_MAX;
    float nb_cells = 'Resolution';
    while (std_dev > 'Subdivision threshold' && it++ < 'Max subdivisions')
    {
        float cell_size = 1. / nb_cells;
        grid_center     = floor(uv * nb_cells) / nb_cells + cell_size / 2.;
        guv             = fract(uv * nb_cells) * 2. - 1.;

        vec2 center1 = grid_center + cell_size * 0.5 * vec2(-1., -1.);
        vec2 center2 = grid_center + cell_size * 0.5 * vec2(-1., +1.);
        vec2 center3 = grid_center + cell_size * 0.5 * vec2(+1., +1.);
        vec2 center4 = grid_center + cell_size * 0.5 * vec2(+1., -1.);

        float val1 = 'Image'(center1);
        float val2 = 'Image'(center2);
        float val3 = 'Image'(center3);
        float val4 = 'Image'(center4);

        float average = (val1 + val2 + val3 + val4) / 4.;
        std_dev       = sqrt(
            sq(val1 - average)
            + sq(val2 - average)
            + sq(val3 - average)
            + sq(val4 - average)
        );
        nb_cells *= 2.;
    }

    float cell_size = 1. / nb_cells;
    float grey      = 'Image'(grid_center);
    float radius    = sqrt(grey > 0.5 ? grey : 1. - grey);
    float dist      = squircle_sdf(guv, radius);
    if (grey < 0.5)
        dist *= -1.;
    return dist;
}